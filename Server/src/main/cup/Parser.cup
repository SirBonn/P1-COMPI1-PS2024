package  srbn.Parser;

import java_cup.runtime.*;
import srbn.Domain.*;

import java.util.ArrayList;
import srbn.Domain.Actions.*;
import srbn.Domain.Errors.*;
import srbn.Domain.Queries.*;
import srbn.Domain.*;
import srbn.Domain.Components.*;

class Parser;

parser code{:

    private ArrayList<ErrorP> errors = new ArrayList<>();
    private ArrayList<Action> actions = new ArrayList<>();
    private ArrayList<Query> queries = new ArrayList<>();

    public void syntax_error(Symbol token) {
        if (token.sym != ParserSym.EOF){
        //System.out.println("sintax error: " +token.value + " en la linea " + token.left);
            addError(token, "Sintax error close at: ");
        }
    }

    public void unrecovered_syntax_error(Symbol token) {
        //System.out.println("unrec sintax error: " +token.value + " en la l√≠nea " + token.right+" en la columna " + token.left);
        addError(token, "unrec error: ");
    }

    private String removeBrckt(String str){
        return str.replace("[", "").replace("]", "");
    }


    public void addError(Symbol token, String msj) {
        try {
            ArrayList<String> list = new ArrayList<>();
            for (Integer ex : expected_token_ids()) {
                list.add(ParserSym.terminalNames[ex]);
            }
            errors.add(new ErrorP(token.left, token.right, token.value.toString(), 1, msj, list));
        } catch (Exception e) {

        }
    }

    public void addAction(Action act){
        actions.add(act);
    }

    public void addQuery(Query query){
        queries.add(query);
    }

    public ArrayList<Action> getActions(){
        return actions;
    }

    public ArrayList<Query> getQueries(){
        return queries;
    }

    public ArrayList<ErrorP> getErrors(){
        return errors;
    }

    public boolean isRight(){
        return errors.isEmpty();
    }

    private int getTypeComp(String type){
            switch(type.toUpperCase()){
                case "TITTLE":
                    return ComponentType.TITTLE.ordinal();
                case "PARAGRAPH":
                    return ComponentType.PARAGRAPH.ordinal();
                case "IMAGE":
                    return ComponentType.IMAGE.ordinal();
                case "VIDEO":
                    return ComponentType.VIDEO.ordinal();
                case "MENU":
                    return ComponentType.MENU.ordinal();
                case "ALL":
                    return ComponentType.ALL.ordinal();
                default:
                    return 99;
            }
        }


        private int getTypeAction(String type){
            switch(type.toUpperCase()){
                case "NEW_SITE":
                    return ActionTypes.NEW_SITE.ordinal();
                case "DELETE_SITE":
                    return ActionTypes.DELETE_SITE.ordinal();
                case "NEW_PAGE":
                    return ActionTypes.NEW_PAGE.ordinal();
                case "DELETE_PAGE":
                    return ActionTypes.DELETE_PAGE.ordinal();
                case "MODIFY_PAGE":
                    return ActionTypes.MODIFY_PAGE.ordinal();
                case "ADD_COMPONENT":
                    return ActionTypes.ADD_COMPONENT.ordinal();
                case "MODIFY_COMPONENT":
                    return ActionTypes.MODIFY_COMPONENT.ordinal();
                case "DELETE_COMPONENT":
                    return ActionTypes.DELETE_COMPONENT.ordinal();
                default:
                    return 99;
            }
        }
:}

non terminal String  parentAttribute, sourceAttribute, colorAttribute, textAttribute, pageParam, modDateParam,
                     userModParam, createDateParam, userCrtParam, parentParam, siteParam, tittleParam, idParam;

non terminal Component   tittleCompDef, paragCompDef, imageCompDef, videoCompDef, menuCompDef, classParamDef;

non terminal Action  newSiteActDef, deleteSiteActDef, newPageActDef, deletePageActDef, modifyPageActDef, addCompActDef,
                     modifyCompActDef, deleteCompActDef, defActionsDecl, opnActionDecl, actionDecl;

non terminal String     alignmentLoc, widthAttribute, heightAttribute, alignmentAttribute, compTypeDecl;

non terminal Label      labelDef;

non terminal ArrayList<Label>   labelDecl, addLabels;

non terminal Query   querysTypeDef, pagesVisitsDef, siteVisitsDef, popularPagesDef, componentDef;

non terminal ArrayList<String>  pathDef;

non terminal         xmlWebCreation, clsTags, querysDecl ;

terminal String      ID_VALUE, TAGID, COLOR_VALUE, DATE_VALUE;

terminal String      NUMBER_VALUE, CENTER_LOC, LEFT_LOC, RIGHT_LOC, JUSTIFY_LOC, HEIGHT, WIDTH;

terminal             DBLEQUOTES, MAYORQ,OPENLABELSTAG,
                     CLOSEACTIONTAG, CLOSEPARAMTAG, CLOSELABELTAG, CLOSELABELSTAG,
                     ACTION,  PARAM, NAME, OPENPARAMSTAG, OPENATTRIBUTESTAG, ATTRIBUTE, OPENLABELTAG, SEMICOLON,
                     CLOSEATTRIBUTESTAG, CLOSEATTRIBUTETAG, CLOSEPARAMSTAG,CLOSEACTIONSTAG, DELETE_COMPONENT,
                     NEW_SITE, DELETE_SITE, NEW_PAGE, DELETE_PAGE, MODIFY_PAGE, ADD_COMPONENT, MODIFY_COMPONENT,
                     ID_PARAM, TITTLE, SITE, PAGE, CLASSTYPE, USER_CREATION, DATE_CREATION, USER_MODIFICATION, DATE_MODIFICATION,
                     PARENT, PARAGRAPH, IMAGE, VIDEO, MENU, TEXT, ALIGNMENT, COLOR, SOURCE, COMMA,
                     ACTIONS, CONSULT, VISITS_PAGE, VISITS_SITE, POPULAR_PAGES, COMPONENT, ALL;

start with xmlWebCreation;



xmlWebCreation ::=  actionDecl:newAction {: addAction(newAction);:}
                |   ACTIONS defActionsDecl:actions CLOSEACTIONSTAG {: System.out.println("actions det");:}
                |   querysDecl:queries {: System.out.println("queries det");:}
                ;

defActionsDecl::=   actionDecl:newAction {: addAction(newAction);:}
                |   defActionsDecl:_newAction actionDecl:newAction {: addAction(newAction);:}
                ;

actionDecl::=       ACTION NAME opnActionDecl:newAction CLOSEACTIONTAG {: RESULT = newAction;:}
                |   error    {::}
                ;

querysDecl::=       CONSULT querysTypeDef:query SEMICOLON  {: addQuery(query);:}
                |   querysDecl:_type CONSULT querysTypeDef:query SEMICOLON {: addQuery(query);:}
                ;

querysTypeDef::=    pagesVisitsDef:pagesVisits      {: RESULT = pagesVisits; :}
                |   siteVisitsDef:siteVisits        {: RESULT = siteVisits; :}
                |   popularPagesDef:popularPages    {: RESULT = popularPages; :}
                |   componentDef:component          {: RESULT = component; :}
                ;

siteVisitsDef::=   VISITS_SITE pathDef:paths                    {:
                                                                    Query query = new Query(0, paths);
                                                                    RESULT = query;
                                                                :}
                ;

pagesVisitsDef::=   VISITS_PAGE pathDef:paths                   {:
                                                                    Query query = new Query(1, paths);
                                                                    RESULT = query;
                                                                :}
                ;

popularPagesDef::=   POPULAR_PAGES pathDef:paths                {:
                                                                    Query query = new Query(2, paths);
                                                                    RESULT = query;
                                                                :}
                ;

componentDef::=   COMPONENT compTypeDecl:compType pathDef:paths
                                                                {:
                                                                    Query query = new Query(3, compType, paths);
                                                                    RESULT = query;
                                                                :}
                ;

pathDef::=        TAGID:id_value                             {:
                                                                    ArrayList<String> paths = new ArrayList<>();
                                                                    paths.add(id_value);
                                                                    RESULT = paths;
                                                                :}
                |   pathDef:paths COMMA TAGID:id_value          {:
                                                                    paths.add(id_value);
                                                                    RESULT = paths;
                                                                :}
                ;

compTypeDecl::=     ALL:compType                             {: RESULT = "0"; :}
                |   TITTLE:compType                          {: RESULT = "1"; :}
                |   PARAGRAPH:compType                       {: RESULT = "2"; :}
                |   IMAGE:compType                           {: RESULT = "3"; :}
                |   VIDEO:compType                           {: RESULT = "4"; :}
                |   MENU:compType                            {: RESULT = "5"; :}
                ;

opnActionDecl::=    newSiteActDef:actionnewSite {: RESULT = actionnewSite; System.out.println("new Site act");:}
                |   deleteSiteActDef:actiondeleteSite {: RESULT = actiondeleteSite; System.out.println("delete site act"); :}
                |   newPageActDef:actionnewPage {: RESULT = actionnewPage; System.out.println("new Page act"); :}
                |   deletePageActDef:actiondeletePage {: RESULT = actiondeletePage; System.out.println("del Page act"); :}
                |   modifyPageActDef:actionmodifyPage {: RESULT = actionmodifyPage; System.out.println("mod Page act"); :}
                |   addCompActDef:actionaddComp {: RESULT = actionaddComp; System.out.println("add comp act");:}
                |   modifyCompActDef:actionmodifyComp {: RESULT = actionmodifyComp; System.out.println("mod comp act"); :}
                |   deleteCompActDef:actiondeleteComp {: RESULT = actiondeleteComp; System.out.println("del comp act"); :}
                |   error:err                           {: :}
                ;


newSiteActDef::=    NEW_SITE:actionType DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:idP
                    userCrtParam:userCrtP
                    createDateParam:creationDateP
                    modDateParam:modDateP
                    userModParam:userModParam
                    CLOSEPARAMSTAG                                                  {:
                                                                                        Action act = new Action(idP.toString(), userCrtP.toString(),
                                                                                            creationDateP.toString(), modDateP.toString(),
                                                                                            userModParam.toString(), getTypeAction("NEW_SITE"));
                                                                                        RESULT = act;
                                                                                    :}
                ;

deleteSiteActDef::= DELETE_SITE DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:idP
                    CLOSEPARAMSTAG                                                       {:
                                                                                        Action act = new Action(idP.toString(),
                                                                                            getTypeAction("DELETE_SITE"));
                                                                                        RESULT = act;
                                                                                    :}
                    ;

newPageActDef::=    NEW_PAGE:actionType DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:idP
                    tittleParam:tittleP
                    siteParam:siteP
                    parentParam:parentP
                    userCrtParam:userCrtP
                    createDateParam:creationDateP
                    userModParam:userModP
                    modDateParam:modDatP
                    CLOSEPARAMSTAG
                    addLabels:lbls                                                  {:
                                                                                        Action act = new Action(idP,
                                                                                            tittleP, siteP, parentP, userCrtP, creationDateP, modDatP, userModP, getTypeAction("NEW_PAGE"));
                                                                                        act.setLabels(lbls);
                                                                                        RESULT = act;
                                                                                    :}
                ;

deletePageActDef::= DELETE_PAGE:actionType DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:id_value
                    CLOSEPARAMSTAG                                                       {:
                                                                                        Action act = new Action(id_value.toString(),
                                                                                            getTypeAction("DELETE_PAGE"));
                                                                                        RESULT = act;
                                                                                    :}
                ;

modifyPageActDef::= MODIFY_PAGE:actionType DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:id_value
                    tittleParam:tittle_value
                    CLOSEPARAMSTAG
                    addLabels:lbls                                                  {:
                                                                                        Action act = new Action(id_value,tittle_value,
                                                                                            getTypeAction("MODIFY_PAGE"));
                                                                                        act.setLabels(lbls);
                                                                                        RESULT = act;
                                                                                    :}
                ;

addCompActDef::=    ADD_COMPONENT DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:id_value
                    pageParam:page_value
                    PARAM NAME CLASSTYPE DBLEQUOTES MAYORQ
                    classParamDef:comp                                              {:
                                                                                        comp.setIdComp(id_value.toString());
                                                                                        comp.setPageComp(page_value.toString());
                                                                                        Action act = new Action(id_value,
                                                                                            page_value, comp, getTypeAction("ADD_COMPONENT"));
                                                                                        RESULT = act;
                                                                                    :}
                ;

modifyCompActDef::= MODIFY_COMPONENT:actionType DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:id_value
                    pageParam:page_value
                    PARAM NAME CLASSTYPE DBLEQUOTES MAYORQ
                    classParamDef:comp                                              {:

                                                                                        comp.setIdComp(id_value.toString());
                                                                                        comp.setPageComp(page_value.toString());
                                                                                        Action act = new Action(id_value,
                                                                                            page_value, comp, getTypeAction("MODIFY_COMPONENT"));
                                                                                        RESULT = act;
                                                                                    :}
                ;

deleteCompActDef::= DELETE_COMPONENT:actionType DBLEQUOTES MAYORQ
                    OPENPARAMSTAG
                    idParam:id_value
                    pageParam:page_value
                    CLOSEPARAMSTAG                                                  {:
                                                                                        Action act = new Action(id_value,
                                                                                            page_value, getTypeAction("DELETE_COMPONENT"));
                                                                                        RESULT = act;
                                                                                    :}
                ;

classParamDef::=    tittleCompDef:tittleComp                                        {: RESULT = tittleComp;:}
                |   paragCompDef:paragComp                                          {: RESULT = paragComp; :}
                |   imageCompDef:imageComp                                          {: RESULT = imageComp; :}
                |   videoCompDef:videoComp                                          {: RESULT = videoComp; :}
                |   menuCompDef:menuComp                                            {: RESULT = menuComp; :}
                ;

tittleCompDef::=    TITTLE:tittle
                    CLOSEPARAMTAG
                    CLOSEPARAMSTAG
                    OPENATTRIBUTESTAG
                    textAttribute:textAttribute
                    alignmentAttribute:alignmentAttribute
                    colorAttribute:colorAttribute
                    CLOSEATTRIBUTESTAG                                              {:
                                                                                        TittleComponent tittleComp = new TittleComponent(getTypeComp("TITTLE"),
                                                                                        textAttribute, colorAttribute, alignmentAttribute);
                                                                                        RESULT = tittleComp;
                                                                                    :}
                ;

paragCompDef::=
                    PARAGRAPH:tittle
                    CLOSEPARAMTAG
                    CLOSEPARAMSTAG
                    OPENATTRIBUTESTAG
                    textAttribute:textAttribute
                    alignmentAttribute:alignmentAttribute
                    colorAttribute:colorAttribute
                    CLOSEATTRIBUTESTAG                                                  {:
                                                                                          ParagraphComponent paragraphComp = new ParagraphComponent(getTypeComp("PARAGRAPH"),
                                                                                          textAttribute.toString(), alignmentAttribute, colorAttribute);
                                                                                          RESULT = paragraphComp;
                                                                                        :}
                ;

imageCompDef::=
                    IMAGE:image
                    CLOSEPARAMTAG
                    CLOSEPARAMSTAG
                    OPENATTRIBUTESTAG
                    sourceAttribute:sourceAttribute
                    alignmentAttribute:alignmentAttribute
                    heightAttribute:heightAttribute
                    widthAttribute:widthAttribute
                    CLOSEATTRIBUTESTAG                                                  {:
                                                                                          ImageComponent imageComp =  new ImageComponent(getTypeComp("IMAGE"),
                                                                                            sourceAttribute.toString(), heightAttribute, widthAttribute, alignmentAttribute);
                                                                                          RESULT = imageComp;
                                                                                        :}
                ;


videoCompDef::=
                    VIDEO:video
                    CLOSEPARAMTAG
                    CLOSEPARAMSTAG
                    OPENATTRIBUTESTAG
                    sourceAttribute:sourceAttribute
                    alignmentAttribute:alignmentAttribute
                    heightAttribute:heightAttribute
                    widthAttribute:widthAttribute
                    CLOSEATTRIBUTESTAG
                                                                                        {:
                                                                                          VideoComponent videoComp = new VideoComponent(getTypeComp("VIDEO"),
                                                                                            sourceAttribute.toString(), heightAttribute, widthAttribute);
                                                                                          RESULT = videoComp;
                                                                                        :}
                ;

menuCompDef::=
                    MENU:menu
                    CLOSEPARAMTAG
                    CLOSEPARAMSTAG
                    OPENATTRIBUTESTAG
                    parentAttribute:parentAttribute
                    CLOSEATTRIBUTESTAG
                    clsTags                                                                 {:
                                                                                              MenuComponent menuComp = new MenuComponent(parentAttribute,
                                                                                                getTypeComp("MENU"));
                                                                                              RESULT = menuComp;
                                                                                            :}
                ;


idParam::=          PARAM NAME ID_PARAM DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

tittleParam::=      PARAM NAME TITTLE DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

siteParam::=        PARAM NAME  SITE DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

parentParam::=      PARAM NAME PARENT DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

userCrtParam::=     PARAM NAME  USER_CREATION DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

createDateParam::=  PARAM NAME DATE_CREATION DBLEQUOTES MAYORQ
                    DATE_VALUE:date_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(date_value);:}
                ;

userModParam::=     PARAM NAME USER_MODIFICATION DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

modDateParam::=     PARAM NAME DATE_MODIFICATION DBLEQUOTES MAYORQ
                    DATE_VALUE:date_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(date_value);:}
                ;

pageParam::=        PARAM NAME PAGE DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEPARAMTAG                                                            {:RESULT= removeBrckt(id_value);:}
                ;

textAttribute::=    ATTRIBUTE NAME TEXT DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEATTRIBUTETAG                                                        {:RESULT= removeBrckt(id_value);:}
                ;

alignmentAttribute::=ATTRIBUTE NAME ALIGNMENT DBLEQUOTES MAYORQ
                    alignmentLoc:alignment_loc
                    CLOSEATTRIBUTETAG                                                        {:RESULT= alignment_loc;:}
                ;

colorAttribute::=   ATTRIBUTE NAME COLOR DBLEQUOTES MAYORQ
                    COLOR_VALUE:id_value
                    CLOSEATTRIBUTETAG                                                        {:RESULT= removeBrckt(id_value);:}
                ;

sourceAttribute::=  ATTRIBUTE NAME SOURCE DBLEQUOTES MAYORQ
                    TAGID:id_value
                    CLOSEATTRIBUTETAG                                                        {:RESULT= removeBrckt(id_value);:}
                ;

heightAttribute::=  ATTRIBUTE NAME HEIGHT DBLEQUOTES MAYORQ
                    NUMBER_VALUE:num_value
                    CLOSEATTRIBUTETAG                                                        {:RESULT= removeBrckt(num_value);:}
                ;

widthAttribute::=   ATTRIBUTE NAME WIDTH DBLEQUOTES MAYORQ
                    NUMBER_VALUE:num_value
                    CLOSEATTRIBUTETAG                                                        {:RESULT= removeBrckt(num_value);:}
                ;

parentAttribute::=  ATTRIBUTE NAME PARENT DBLEQUOTES MAYORQ
                    ID_VALUE:id_value
                    CLOSEATTRIBUTETAG                                                        {:RESULT= removeBrckt(id_value);:}
                ;

alignmentLoc::=       CENTER_LOC:center_loc                                                  {:RESULT= "1"; :}
                |     LEFT_LOC:left_loc                                                      {:RESULT= "0"; :}
                |     RIGHT_LOC:right_loc                                                    {:RESULT= "2"; :}
                |     JUSTIFY_LOC:justify_loc                                                {:RESULT= "3"; :}
                ;

addLabels::=        OPENLABELSTAG labelDecl:labels CLOSELABELSTAG                           {:RESULT = labels;:}
                |                                                                           {:RESULT = null;:}
                ;

labelDecl::=         labelDef:tag                                                           {:  ArrayList<Label> labels = new ArrayList<>();
                                                                                                labels.add(tag);
                                                                                                RESULT = labels;
                                                                                            :}
                |    labelDecl:tagDef labelDef:tag                                          {: tagDef.add(tag);
                                                                                            RESULT = tagDef;
                                                                                            :}
                ;

labelDef::=          OPENLABELTAG TAGID:id_val CLOSELABELTAG                                {:RESULT = new Label(id_val);:}
                ;